/*
 * Copyright (C) 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

task wrapper(type: Wrapper) {
  gradleVersion = "1.9"
}

project.ext {
  rosMavenDeploymentRepository = System.getenv("ROS_MAVEN_DEPLOYMENT_REPOSITORY")
  rosMavenRepository = System.getenv("ROS_MAVEN_REPOSITORY")
  rosMavenPath = System.getenv("ROS_MAVEN_PATH")
}

allprojects {
  group = "org.ros.rosjava_bootstrap"
  version = "0.2.0"
}

subprojects {
  apply plugin: "eclipse"
  apply plugin: "java"
  apply plugin: "maven"
  apply plugin: "maven-publish"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  repositories {
    if (project.rosMavenPath != null) {
      project.rosMavenPath.tokenize(":").each { path ->
        maven {
          url uri(path)
        }
      }
    }
    if (project.rosMavenRepository != null) {
      maven {
        url project.rosMavenRepository
      }
    }
    mavenLocal()
    maven {
      url "http://repository.springsource.com/maven/bundles/release"
    }
    maven {
      url "http://repository.springsource.com/maven/bundles/external"
    }
    mavenCentral()
  }
  if (project.rosMavenDeploymentRepository != null &&
      project.rosMavenDeploymentRepository != "") {
    publishing {
      publications {
        mavenJava(MavenPublication) {
          from components.java
        }
      }
      repositories {
        maven {
           url uri(project.rosMavenDeploymentRepository)
        }
      }
    }
  }
}

defaultTasks "install"
